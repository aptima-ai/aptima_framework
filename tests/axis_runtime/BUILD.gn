#
# Copyright Â© 2025 Agora
# This file is part of APTIMA Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/aptima_runtime/options.gni")
import("//build/aptima_runtime/aptima.gni")
import("//core/src/aptima_runtime/output_libs.gni")
import("//core/src/aptima_utils/output_libs.gni")
import("//third_party/curl/output_libs.gni")
import("//third_party/jansson/output_libs.gni")
import("//third_party/libwebsockets/output_libs.gni")
import("//third_party/mbedtls/output_libs.gni")
import("//third_party/zlib/output_libs.gni")

# Because there is no 'rpath' mechanism in Windows, we need to copy all the
# dependend libraries of 'smoke' to one folder, and setup 'PATH' before
# executing 'smoke' and 'unit' testing.
copy("copy_libraries_for_test") {
  sources = aptima_runtime_output_libs
  sources += aptima_utils_output_libs

  deps = [
    "//core/src/aptima_runtime",
    "//core/src/aptima_utils:aptima_utils_shared",
    "//tests:copy_jansson_library_for_test",
  ]

  if (aptima_enable_curl) {
    sources += curl_output_libs
    deps += [ "//third_party/curl" ]

    sources += zlib_output_libs
    deps += [ "//third_party/zlib" ]
  }

  if (aptima_enable_libwebsockets) {
    sources += mbedtls_output_libs
    sources += libwebsockets_output_libs

    deps += [
      "//third_party/libwebsockets",
      "//third_party/mbedtls",
    ]
  }

  outputs = [ "${root_out_dir}/tests/standalone/{{source_file_part}}" ]
}

group("aptima_runtime") {
  deps = []

  if (aptima_enable_tests && aptima_enable_unit_tests) {
    deps += [ "unit" ]
  }

  if (aptima_enable_tests && aptima_enable_smoke_tests) {
    deps += [ "smoke" ]
  }

  if (aptima_enable_tests && aptima_enable_integration_tests &&
      # Almost all integration test cases needs aptima_manager.
      aptima_enable_aptima_manager) {
    deps += [ "integration" ]
  }
}
