#
# Copyright Â© 2025 Agora
# This file is part of APTIMA Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/axis_runtime/aptima.gni")

config("axis_runtime_python_config") {
  include_dirs = axis_runtime_common_includes

  python_version = "3"  # "3" by default

  cflags = exec_script("//build/axis_common/python/python_config.py",
                       [
                         "--python-version",
                         python_version,
                         "--target-os",
                         target_os,
                         "--config-type",
                         "cflags",
                         "--log-level",
                         "0",
                       ],
                       "list lines")
  if (!is_win) {
    cflags += [ "-fPIC" ]
  }

  ldflags = exec_script("//build/axis_common/python/python_config.py",
                        [
                          "--python-version",
                          python_version,
                          "--target-os",
                          target_os,
                          "--config-type",
                          "ldflags",
                          "--log-level",
                          "0",
                        ],
                        "list lines")

  if (is_mac) {
    # Must be renamed to python module name.
    ldflags += [
      "-install_name",
      "axis_runtime_python.so",
    ]
  }

  # Add rpath to find axis_runtime library.
  if (is_mac) {
    ldflags += [ "-Wl,-rpath,@loader_path/../../axis_runtime/lib" ]
  } else if (is_linux) {
    ldflags += [ "-Wl,-rpath=\$ORIGIN/../../axis_runtime/lib" ]
  }

  # Only export "aptima" relevant symbols.
  if (is_mac) {
    ldflags += [
      "-Xlinker",
      "-exported_symbols_list",
      "-Xlinker",
      rebase_path("//build/axis_runtime/ld_script/mac_for_python_binding"),
    ]
  } else if (is_linux) {
    ldflags += [ "-Wl,--version-script=" + rebase_path(
                     "//build/axis_runtime/ld_script/linux_for_python_binding") ]
  }

  libs = exec_script("//build/axis_common/python/python_config.py",
                     [
                       "--python-version",
                       python_version,
                       "--target-os",
                       target_os,
                       "--config-type",
                       "libs",
                       "--log-level",
                       "0",
                     ],
                     "list lines")
}

axis_shared_library("axis_runtime_python") {
  configs = [ ":axis_runtime_python_config" ]

  if (is_mac || is_linux) {
    add_configs = [ "//.gnfiles/build/toolchain/common:allow_undefined" ]
    remove_configs = [ "//.gnfiles/build/toolchain/common:disallow_undefined" ]
  }

  sources = [
    "//core/src/axis_runtime/binding/python/native/build_template/preserved_metadata.c",
    "init.c",
  ]

  deps = [
    "addon",
    "app",
    "common",
    "extension",
    "msg",
    "axis_env",
    "test",
    "//core/src/axis_runtime:axis_runtime_library",
  ]

  if (is_mac) {
    # According to the explanation in https://bugs.python.org/issue43898, even
    # on macOS, when Python imports a Python C extension, the file extension
    # must be `.so` and cannot be `.dylib`.
    output_extension = "so"
  }
}
