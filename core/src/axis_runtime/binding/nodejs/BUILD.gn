#
# Copyright Â© 2025 Agora
# This file is part of APTIMA Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/feature/axis_package.gni")
import("//build/options.gni")
import("//build/axis_runtime/feature/javascript.gni")
import("//build/axis_runtime/feature/publish.gni")
import("//build/axis_runtime/options.gni")
import("//build/axis_runtime/aptima.gni")
import("//core/src/axis_runtime/output_libs.gni")

nodejs_library("axis_runtime_nodejs_js") {
  tsconfig = "tsconfig.json"
  package_json = "package.json"
  output_dir = "$root_out_dir/axis_packages/system/axis_runtime_nodejs"
  remove_node_modules = true
  remove_tsbuildinfo = true
  remove_src = true
}

axis_package("axis_nodejs_binding_system_package") {
  package_kind = "system"
  package_output_root_dir_name = "axis_runtime_nodejs"

  resources = [
    "//core/src/axis_runtime/binding/nodejs/interface=>interface",
    "manifest.json",
    "package.json",
    "tsconfig.json",
  ]

  foreach(lib, axis_runtime_nodejs_output_libs) {
    libname = get_path_info(rebase_path(lib), "file")
    resources += [ "${lib}=>lib/${libname}" ]
  }

  deps = [
    ":axis_runtime_nodejs_js",
    "native:axis_runtime_nodejs",
  ]
}

if (axis_enable_axis_manager) {
  axis_package_publish("upload_axis_nodejs_binding_system_package_to_server") {
    base_dir =
        rebase_path("${root_out_dir}/axis_packages/system/axis_runtime_nodejs")
    deps = [ ":axis_nodejs_binding_system_package" ]
  }
}

group("nodejs") {
  deps = [
    ":axis_runtime_nodejs_js",
    "native:axis_runtime_nodejs",
  ]

  if (axis_enable_axis_manager) {
    deps += [ ":upload_axis_nodejs_binding_system_package_to_server" ]
  }
}
