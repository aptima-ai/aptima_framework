#
# Copyright Â© 2025 Agora
# This file is part of APTIMA Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/feature/axis_package.gni")
import("//build/options.gni")
import("//build/axis_runtime/feature/publish.gni")
import("//build/axis_runtime/feature/axis_go_lint.gni")
import("//build/axis_runtime/options.gni")
import("//core/src/axis_runtime/output_libs.gni")

if (axis_enable_go_lint) {
  axis_go_lint("axis_go_binding_lint") {
    # The module path must contain .go files.
    lint_dir = "//core/src/axis_runtime/binding/go/interface/aptima"
    go_exec = "go1.20.12"
  }
}

axis_package("axis_go_binding_system_package") {
  package_kind = "system"
  package_output_root_dir_name = "axis_runtime_go"

  resources = [
    "//core/src/axis_runtime/binding/go/interface=>interface",
    "//core/src/axis_runtime/binding/go/tools=>tools",
    "BUILD_release.gn=>BUILD.gn",
    "manifest.json",
  ]

  foreach(lib, axis_runtime_go_output_libs) {
    libname = get_path_info(rebase_path(lib), "file")
    resources += [ "${lib}=>lib/${libname}" ]
  }

  deps = [ "native:axis_runtime_go" ]

  if (axis_enable_go_lint) {
    deps += [ ":axis_go_binding_lint" ]
  }
}

if (axis_enable_axis_manager) {
  axis_package_publish("upload_axis_go_binding_system_package_to_server") {
    base_dir = rebase_path("${root_out_dir}/axis_packages/system/axis_runtime_go")
    deps = [
      ":axis_go_binding_system_package",
      "native:axis_runtime_go",
    ]
  }
}

group("go") {
  deps = [ "native:axis_runtime_go" ]

  if (axis_enable_axis_manager) {
    deps += [ ":upload_axis_go_binding_system_package_to_server" ]
  }
}
