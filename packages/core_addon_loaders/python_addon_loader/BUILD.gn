#
# This file is part of APTIMA Framework, an open source project.
# Licensed under the Apache License, Version 2.0.
# See the LICENSE file for more information.
#
import("//build/feature/axis_package.gni")
import("//build/axis_runtime/feature/publish.gni")
import("//build/axis_runtime/glob.gni")
import("//build/axis_runtime/options.gni")

axis_package("python_addon_loader") {
  package_kind = "addon_loader"
  enable_build = true

  resources = [
    "manifest.json",
    "property.json",
  ]

  sources = [ "src/main.cc" ]
  include_dirs = [ "//core" ]

  ldflags = []
  if (is_mac) {
    ldflags +=
        [ "-Wl,-rpath,@loader_path/../../../system/axis_runtime_python/lib" ]
  } else if (is_linux) {
    ldflags += [
      "-Wl,-rpath=\$ORIGIN/../../../system/axis_runtime_python/lib",
      "-Wl,-rpath-link=\$ORIGIN/../../../system/axis_runtime_python/lib",
    ]
  }

  deps = [
    "//core/src/axis_runtime",
    "//core/src/axis_runtime/binding/python",
  ]
}

if (axis_enable_axis_manager) {
  axis_package_publish("upload_python_addon_loader_to_server") {
    base_dir = rebase_path(
            "${root_out_dir}/axis_packages/addon_loader/python_addon_loader")
    deps = [ ":python_addon_loader" ]
  }
}
